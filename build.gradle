plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
}

base { archivesName = project.mod_id }
version = mod_version
group = mod_group

repositories {
	maven {
		url 'https://maven.ladysnake.org/releases'
		content { includeGroup "dev.onyxstudios.cardinal-components-api" }
	}
}

loom {
    splitEnvironmentSourceSets()

	runs {
		client { source = sourceSets.test }
		server { source = sourceSets.test }
	}
}

sourceSets.test {
	runtimeClasspath += sourceSets.main.runtimeClasspath
	runtimeClasspath += sourceSets.client.runtimeClasspath
	compileClasspath += sourceSets.main.compileClasspath
	compileClasspath += sourceSets.client.compileClasspath
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	include modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}")
	include modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_components_version}")
}

processResources {
	filesMatching("fabric.mod.json") {
		expand project.properties
	}
}

java {
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}
build.dependsOn publishToMavenLocal
